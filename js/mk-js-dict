#!/usr/bin/perl
# Make a javascript trie representing a dictionary
# James Stanley 2013

use strict;
use warnings;

use JSON;

my $dictionary = {};

while (<>) {
    chomp;

    add_word($_);
}

my $jsondict = encode_json($dictionary);
$jsondict =~ s/"//g;
my $y1 = '{0:1,s:{0:1}}';
$jsondict =~ s/\Q$y1\E/y1/g;
my $y2 = '{0:1}';
$jsondict =~ s/\Q$y2\E/y2/g;
print "var y1=$y1;var y2=$y2;var dictionary=$jsondict;\n";

sub add_word {
    my ($word) = @_;

    my $n = $dictionary;

    while (length $word) {
        # TODO: deal with invalid letters

        $word =~ s/^(.)//;
        my $c = $1;

        if (!$n->{$c}) {
            $n->{$c} = {};
        }

        $n = $n->{$c};
    }

    $n->{0} = 1;
}
